#!/usr/bin/perl -w

#
# Copyright (c) 2000-2014 University of Utah and the Flux Group.
# 
# {{{EMULAB-LICENSE
# 
# This file is part of the Emulab network testbed software.
# 
# This file is free software: you can redistribute it and/or modify it
# under the terms of the GNU Affero General Public License as published by
# the Free Software Foundation, either version 3 of the License, or (at
# your option) any later version.
# 
# This file is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
# FITNESS FOR A PARTICULAR PURPOSE.  See the GNU Affero General Public
# License for more details.
# 
# You should have received a copy of the GNU Affero General Public License
# along with this file.  If not, see <http://www.gnu.org/licenses/>.
# 
# }}}
#

package libaudit;
use Exporter;

@ISA = "Exporter";
@EXPORT =
    qw ( AuditStart AuditEnd AuditAbort AuditFork AuditSetARGV AuditGetARGV
	 AddAuditInfo
	 LogStart LogEnd LogAbort AuditDisconnect
	 LIBAUDIT_NODAEMON LIBAUDIT_DAEMON LIBAUDIT_LOGONLY
	 LIBAUDIT_NODELETE LIBAUDIT_FANCY LIBAUDIT_LOGTBOPS LIBAUDIT_LOGTBLOGS
       );

# After package decl.
use English;
use POSIX qw(isatty setsid dup2);
use File::Basename;
use IO::Handle;
use Carp;

#
# Testbed Support libraries
#
use libtestbed;

my $TBOPS	= "@TBOPSEMAIL@";
my $TBAUDIT	= "@TBAUDITEMAIL@";
my $TBLOGS	= "@TBLOGSEMAIL@";
my $OURDOMAIN   = "@OURDOMAIN@";
my $SCRIPTNAME	= "Unknown";
my $USERNAME    = "Unknown";
my $GCOS        = "Unknown";
my @SAVEARGV	= @ARGV;
my $SAVEPID	= $PID;
my $SAVE_STDOUT = 0;
my $SAVE_STDERR = 0;

# Indicates, this script is being audited.
my $auditing	= 0;

# Where the log is going. When not defined, do not send it in email!
my $logfile;

# Logonly, not to audit list.
my $logonly     = 0;
# Log to tbops or tblogs
my $logtblogs   = 0;

# Save log when logging only.
my $savelog     = 0;

# If set than send "fancy" email and also call tblog_find_error
# on errors
my $fancy       = 0;

# Extra info used when AUDIT_FANCY is set
my %AUDIT_INFO;

# Untainted scriptname for email below.
if ($PROGRAM_NAME =~ /^([-\w\.\/]+)$/) {
    $SCRIPTNAME = basename($1);
}
else {
    $SCRIPTNAME = "Tainted";
}

# The user running the script.
if (my ($name,undef,undef,undef,undef,undef,$gcos) = getpwuid($UID)) {
    $USERNAME = $name;
    $GCOS     = $gcos;
}

#
# Options to AuditStart.
#
sub LIBAUDIT_NODAEMON	{ 0; }
sub LIBAUDIT_DAEMON	{ 0x01; }
sub LIBAUDIT_LOGONLY	{ 0x02; }
sub LIBAUDIT_NODELETE	{ 0x04; }
sub LIBAUDIT_FANCY      { 0x08; } # Only use if libdb and libtblog are
                                  # already in use
sub LIBAUDIT_LOGTBOPS	{ 0x10; }
sub LIBAUDIT_LOGTBLOGS	{ 0x20; }

#
# Start an audit (or log) of a script. First arg is a flag indicating if
# the script should fork/detach. The second (optional) arg is a file name
# into which the log should be written. The return value is non-zero in the
# parent, and zero in the child (if detaching).
# 
sub AuditStart($;$$)
{
    my($daemon, $logname, $options) = @_;

    #
    # If we are already auditing, then do not audit a child script. This
    # would result in a blizzard of email! We wrote the scripts, so we
    # should now what they do!
    #
    if (defined($ENV{'TBAUDITON'})) {
	return;
    }

    # Logging instead of "auditing" ...
    if (defined($options)) {
	if ($options & LIBAUDIT_NODELETE()) {
	    $savelog = 1;
	}
	if ($options & LIBAUDIT_LOGONLY()) {
	    $logonly = 1;

	    if ($options & LIBAUDIT_LOGTBOPS()) {
		$logtbops = 1;
	    }
	    elsif ($options & LIBAUDIT_LOGTBLOGS()) {
		$logtblogs = 1;
	    }
	}
	if ($options & LIBAUDIT_FANCY()) {
	    if (!$INC{"libdb.pm"} || !$INC{"libtblog.pm"}) {
		croak "libdb and libtblog must be loaded when using LIBAUDIT_FANCY";
	    }
	    $fancy = 1;
	}
    }

    #
    # If this is an interactive session, then do not bother with a log
    # file. Just send it to the output and hope the user is smart enough to
    # save it off. We still want to audit the operation though, sending a
    # "what was done" message to the audit list, and CC it to tbops if it
    # exits with an error. But the log is the responsibility of the user.
    #
    if (!$daemon && isatty(STDIN)) {
	$auditing = 1;
	$ENV{'TBAUDITON'} = "$SCRIPTNAME:$USERNAME";
	return;
    }

    if (!defined($logname)) {
	$logfile = TBMakeLogname("$SCRIPTNAME");
    }
    else {
	$logfile = $logname;
    }
    $ENV{'TBAUDITLOG'} = $logfile;
    $ENV{'TBAUDITON'}  = "$SCRIPTNAME:$USERNAME";

    #
    # Okay, daemonize.
    #
    if ($daemon) {
	my $mypid = fork();
	if ($mypid) {
	    select(undef, undef, undef, 0.2);
	    return $mypid;
	}
	if (defined(&libtblog::tblog_new_child_process)) {
	    libtblog::tblog_new_child_process();
	}
    }
    $auditing = 1;

    #
    # If setuid, lets reset the owner/mode of the log file. Otherwise its
    # owned by root, mode 600 and a pain to deal with later, especially if
    # the script drops its privs!
    #
    if ($UID != $EUID) {
	chown($UID, $EUID, $logfile);
	chmod(0664, $logfile);
    }

    # Save old stderr and stdout.
    if (!$daemon && $PERL_VERSION >= 5.008) {
	eval("open(OLDOUT, \">&\", \*STDOUT); ".
	     "\$libaudit::SAVE_STDOUT = *OLDOUT; ".
	     "open(OLDERR, \">&\", \*STDERR); ".
	     "\$libaudit::SAVE_STDERR = *OLDERR;");
    }

    open(STDOUT, ">> $logfile") or
	die("opening $logfile for STDOUT: $!");
    open(STDERR, ">> $logfile") or
	die("opening $logfile for STDERR: $!");

    #
    # Turn off line buffering on output
    #
    STDOUT->autoflush(1);
    STDERR->autoflush(1);

    if ($daemon) {
	#
	# We have to disconnect from the caller by redirecting both
	# STDIN and STDOUT away from the pipe. Otherwise the caller
	# will continue to wait even though the parent has exited.
	#
	open(STDIN, "< /dev/null") or
	    die("opening /dev/null for STDIN: $!");

	#
	# Create a new session to ensure we are clear of any process group
	#
        POSIX::setsid() or
	    die("setsid failed: $!");
    }

    return 0;
}

# Logging, not auditing.
sub LogStart($;$$)
{
    my($daemon, $logname, $options) = @_;
    $options = 0
	if (!defined($options));

    return AuditStart($daemon, $logname, $options|LIBAUDIT_LOGONLY());
}

sub LogEnd(;$)
{
    my ($status) = @_;
    
    return AuditEnd($status);
}

sub LogAbort()
{
    return AuditAbort();
}

#
# Finish an Audit. 
#
sub AuditEnd(;$)
{
    my ($status) = @_;

    $status = 0
	if (!defined($status));
    
    SendAuditMail($status);
    delete @ENV{'TBAUDITLOG', 'TBAUDITON'};
    unlink($logfile)
	if (defined($logfile) && !$savelog);
    return 0;
}

#
# Overwrite our saved argv. Usefull when script contains something that
# should not go into a mail log.
#
sub AuditSetARGV(@)
{
    @SAVEARGV = @_;
}
sub AuditGetARGV()
{
    return @SAVEARGV;
}

sub AuditDisconnect()
{
    if ($auditing) {
	if (!$daemon && $PERL_VERSION >= 5.008 && $libaudit::SAVE_STDOUT) {
	    close($libaudit::SAVE_STDOUT);
	    close($libaudit::SAVE_STDERR);

	    open(FOO, "> /dev/null");
	    $libaudit::SAVE_STDOUT = *FOO;
	    $libaudit::SAVE_STDERR = *FOO;
	}
    }
}

#
# Abort an Audit. Dump the log file and do not send email.
#
sub AuditAbort()
{
    if ($auditing) {
	$auditing = 0;

	if (!$daemon && $PERL_VERSION >= 5.008 && $libaudit::SAVE_STDOUT) {
	    eval("open(STDOUT, \">&\", \$libaudit::SAVE_STDOUT); ".
		 "open(STDERR, \">&\", \$libaudit::SAVE_STDERR);");
	}

	if (defined($logfile)) {
	    #
	    # This should be okay; the process will keep writing to it,
	    # but will be deleted once the process ends and its closed.
	    #
	    unlink($logfile)
	    	if (!$savelog);
	    undef($logfile);
	}
	delete @ENV{'TBAUDITLOG', 'TBAUDITON'};
    }
    return 0;
}

#
# Ug, forked children result in multiple copies. It does not happen often
# since most forks result in an exec.
#
sub AuditFork()
{
    return 0
	if (!$auditing || !defined($logfile));

    open(LOG, ">> $logfile") or
	die("opening $logfile for $logfile: $!");
	
    close(STDOUT);
    close(STDERR);
    POSIX::dup2(fileno(LOG), 1);
    POSIX::dup2(fileno(LOG), 2);
    STDOUT->fdopen(1, "a");
    STDERR->fdopen(2, "a");
    close(LOG);

    #
    # Turn off line buffering on output
    #
    STDOUT->autoflush(1);
    STDERR->autoflush(1);

    #
    # Need to close these so that this side of the fork is disconnected.
    # Do NOT close the saved STDOUT/STDERR descriptors until the new
    # ones are open and dup'ed into fileno 1 and 2, and the LOG descriptor
    # closed. This was causing SelfLoader to get confused abut something!
    #
    if (!$daemon && $PERL_VERSION >= 5.008) {
	close($libaudit::SAVE_STDOUT)
	    if ($libaudit::SAVE_STDOUT);
	close($libaudit::SAVE_STDERR)
	    if ($libaudit::SAVE_STDERR);
	$libaudit::SAVE_STDOUT = 0;
	$libaudit::SAVE_STDERR = 0;
    }

    #
    # We have to disconnect STDIN from the caller too.
    #
    open(STDIN, "< /dev/null") or
	die("opening /dev/null for STDIN: $!");

    #
    # Create a new session to ensure we are clear of any process group.
    #
    POSIX::setsid();
    
    return 0;
}

#
# Internal function to send the email. First argument is exit status.
#
# Two messages are sent. A topical message is sent to the audit list. This
# is a short message that says what was done and by who. The actual log of
# what happened is sent to the logs list so that we can go back and see the
# details if needed.
# 
sub SendFancyMail($);
sub SendAuditMail($)
{
    my($exitstatus) = @_;
    
    if ($auditing) {
	# Avoid duplicate messages.
	$auditing = 0;

	# Needs to called here before STDOUT and STDERR is
	# redirectected below
	if ($exitstatus && $fancy) {
	    &libtblog::tblog_find_error(); 
	}

	if (!$daemon && $PERL_VERSION >= 5.008 && $libaudit::SAVE_STDOUT) {
	    eval("open(STDOUT, \">&\", \$libaudit::SAVE_STDOUT); ".
		 "open(STDERR, \">&\", \$libaudit::SAVE_STDERR);");
	}

	my $subject  = "$SCRIPTNAME @SAVEARGV";
	if ($exitstatus) {
	    $subject = "Failed: $subject";
	}

	my $body     = "$SCRIPTNAME @SAVEARGV\n" .
	               "Invoked by $USERNAME ($GCOS)";
	if ($exitstatus) {
	    $body   .= "\nExited with status: $exitstatus";
	}
	if (defined($AUDIT_INFO{'message'})) {
	    $body   .= "\n" . $AUDIT_INFO{'message'};
	}
	my $FROM     = "$GCOS <${USERNAME}\@${OURDOMAIN}>";

	if (! $logonly) {
	    SENDMAIL($TBAUDIT, $subject, $body, $FROM, undef, ());
	}

	# Success and no log ...
	if ($exitstatus == 0 && !(defined($logfile) && -s $logfile)) {
	    # Do not save empty logfile. 
	    unlink($logfile)
		if (defined($logfile));
	    return;
	}

	if ($fancy) {
	    SendFancyMail($exitstatus);
	    return;
	}

	#
	# Send logfile to tblogs. Carbon to tbops if it failed. If no logfile
	# then no point in sending to tblogs, obviously.
	#
	my $TO;
	my $HDRS  = "Reply-To: $TBOPS";
	my @FILES = ();
	
	if (defined($logfile) && -s $logfile) {
	    @FILES = ($logfile);

	    if ($logonly) {
		if (defined($AUDIT_INFO{'to'})) {
		    $TO    = join(', ', @{ $AUDIT_INFO{'to'} });
		}
		elsif ($logtbops) {
		    $TO    = $TBOPS;
		}
		elsif ($logtblogs) {
		    $TO    = $TBLOGS;
		    $HDRS .= "\nCC: $TBOPS" if ($exitstatus);
		}
		else {
		    $TO    = $FROM;
		    $HDRS .= "\nCC: ". ($exitstatus ? $TBOPS : $TBLOGS);
		}
	    }
	    else {
		$TO    = $TBLOGS;
		$HDRS .= "\nCC: $TBOPS" if ($exitstatus);
	    }
	}
	elsif ($logtblogs) {
	    $TO    = $TBLOGS;
	    $HDRS .= "\nCC: $TBOPS" if ($exitstatus);
	}
	else {
	    $TO    = $TBOPS;
	}
	if (defined($AUDIT_INFO{'cc'})) {
	    $HDRS .= "\n";
	    $HDRS .= "CC: " . join(', ', @{ $AUDIT_INFO{'cc'} });
	}

	# Leave logfile if sendmail fails. 
	if (SENDMAIL($TO, $subject, $body, $FROM, $HDRS, @FILES)) {
	    unlink($logfile)
		if (defined($logfile) && !$savelog);
	}
    }
}

sub SendFancyMail($)
{
    import libdb;
    import libtblog;
    import User;

    my ($exitstatus) = @_;
    
    my ($TO, $FROM);
    my ($name, $email);
    my $this_user = User->ThisUser();
    if (defined($this_user)) {
	$name  = $this_user->name();
	$email = $this_user->email();
	$TO    = "$name <$email>";
    } else {
	$TO = "$GCOS <${USERNAME}\@${OURDOMAIN}>";
    }
    $FROM = $TO;

    my @FILES;
    
    if (defined($logfile) && -s $logfile) {
	@FILES = ($logfile);
    }

    # Avoid sending a person the same email twice
    my $extra_cc;
    if (defined ($AUDIT_INFO{cc})) {
	my @cc;
	my @prev_emails = ($email);
	OUTER: foreach (@{$AUDIT_INFO{cc}}) {
	    ($email) = /([^<> \t@]+@[^<> \t@]+)/;
	    foreach my $e (@prev_emails) {
		next OUTER if $email eq $e;
		push @prev_email, $e;
	    }
	    push @cc, $_;
	}
	if (@cc) {
	    $extra_cc = "Cc: ";
	    $extra_cc .= join(', ', @cc);
	}
    }

    my $sendmail_res;
    if ($exitstatus) {
	my $d = tblog_lookup_error();
	my $prefix;
	$prefix .= "$SCRIPTNAME @SAVEARGV\n";
	$prefix .= "Exited with status: $exitstatus";
	my $what = "Failed: $SCRIPTNAME";
	$what = $AUDIT_INFO{failure_frag} if defined $AUDIT_INFO{failure_frag};
	$which = $AUDIT_INFO{which};
	$sendmail_res 
	    = tblog_email_error($d, $TO, $what,	$which, 
				$FROM, $extra_cc, "Cc: $TBOPS",
				$prefix, @FILES); 

    } else {

	my $subject  = "$SCRIPTNAME succeeded";
	$subject = $AUDIT_INFO{success_frag} if defined $AUDIT_INFO{success_frag};
	$subject .= ": $AUDIT_INFO{which}" if defined $AUDIT_INFO{which};
	my $body     = "$SCRIPTNAME @SAVEARGV\n";

	my $HDRS;
	$HDRS .= "$extra_cc\n" if defined $extra_cc;
	$HDRS .= "Reply-To: $TBOPS\n";
	$HDRS .= "Bcc: $TBLOGS";
	
	$sendmail_res 
	    = SENDMAIL($TO, $subject, $body, $FROM, $HDRS, @FILES);
    }
    
    if ($sendmail_res) {
	unlink($logfile)
	    if (defined($logfile) && !$savelog);
    }
}


# Info on possibe values for AUDIT_INFO
# [KEY => string|list]
my %AUDIT_METAINFO = 
    ( which => 'string',        # ex "PROJ/EXP"
      success_frag => 'string', # ex "T. Swapped In"
      failure_frag => 'string', # ie "Bla Failure"
      message      => 'string',
      to           => 'list',   # Send audit mail to these people
      cc           => 'list');  # Cc audit mail to these people

#
# AddAuditInfo($key, $value)
#   add additional information for libaudit to use in SendAuditMail
#   when AUDIT_FANCY is set
#
# TODO: Eventually child scripts should be able to use AddAuditInfo, not 
#   just the script in which AuditStart(...) was called.  This will probably
#   involve storing the values in the database somehow.
#
sub AddAuditInfo ($$) {
    my ($key, $value) = @_;

    if (!$auditing) {

	carp "AddAuditInfo($key, ...) ignored since the script isn't being audited.";
	return 0;

    }

    if ($AUDIT_METAINFO{$key} eq 'string') {
	
	$AUDIT_INFO{$key} = $value;
	return 1;

    } elsif ($AUDIT_METAINFO{$key} eq 'list') {

	push @{$AUDIT_INFO{$key}}, $value;
	return 1;

    } else {

	carp "Unknown key, \"$key\" in AddAuditInfo";
	return 0;

    }
}

#
# When the script ends, if the audit has not been sent, send it. 
# 
END {
    # Save, since shell commands will alter it.
    my $exitstatus = $?;
    
    SendAuditMail($exitstatus);

    $? = $exitstatus;
}

1;
